plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'jacoco'
    id "com.jfrog.bintray" version "1.8.4"
    id "org.jetbrains.dokka" version "0.9.18"
}

apply plugin: 'maven-publish'

group 'io.saeid.sweep'
version '0.5.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.google.code.gson:gson:2.8.5"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.mockk:mockk:1.9'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dokka {
    outputFormat 'javadoc'
    outputDirectory "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    classifier 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifactId 'sweep-gson'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            version version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    publications = ['MyPublication']
    pkg {
        repo = "maven"
        name = "sweep-gson"
        userOrg = ""
        websiteUrl = "https://github.com/SaeedMasoumi/sweep-gson/"
        vcsUrl = "https://github.com/SaeedMasoumi/sweep-gson/"
        setLabels("kotlin", "gson")
    }
}